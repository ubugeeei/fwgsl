-- Shadorial Chapter 07: Noise Wave
-- 1D value noise with cubic Hermite interpolation

hash : F32 -> F32
hash n = $fract ($sin n * 43758.5453123)

noise1d : F32 -> F32
noise1d x = $mix (hash i) (hash (i + 1.0)) u
  where
    i = $floor x
    f = $fract x
    u = f * f * (3.0 - 2.0 * f)

@fragment
main : Vec 2 F32 -> F32 -> Vec 2 F32 -> Vec 4 F32
main fragCoord time resolution = $vec4 ($vecX color'') ($vecY color'') ($vecZ color'') 1.0
  where
    uv        = fragCoord / resolution
    x         = $vecX uv * 8.0
    y         = $vecY uv * 2.0 - 1.0
    noiseWave = noise1d (x * 2.0 + time) * 0.6 - 0.3
              + noise1d (x * 5.0 - time * 0.7) * 0.2
    lineWidth = 0.03
    dist      = $abs (y - noiseWave)
    line      = 1.0 - $smoothstep 0.0 lineWidth dist
    sineWave  = 0.3 * $sin (x * 2.0 + time)
    distSine  = $abs (y - sineWave)
    lineSine  = 1.0 - $smoothstep 0.0 (lineWidth * 0.7) distSine
    axis      = 1.0 - $smoothstep 0.0 0.005 ($abs y)
    bgNoise   = noise1d ($vecX uv * 10.0 + time * 0.3) * 0.1
    bgColor   = $vec3 (0.05 + bgNoise) (0.05 + bgNoise) (0.05 + bgNoise)
    color     = $mix bgColor ($vec3 0.3 0.3 0.3) (axis * 0.4)
    color'    = $mix color ($vec3 0.3 0.3 0.5) (lineSine * 0.4)
    color''   = $mix color' ($vec3 0.4 0.9 0.8) line
